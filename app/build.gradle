plugins {
    id 'application'
}

application {
    mainClassName = 'net.bdavies.BabbleBot'
    applicationName = 'Babblebot'
}


project.startScripts {
    doLast {
        String agentFileName = 'babblebot-server-db-' + project(':db').getVersion() + '.jar'
        String forwardSlash = "/"
        String unixRegex = $/exec "$$JAVACMD" /$
        String unixReplacement = $/exec "$$JAVACMD" -javaagent:"$$APP_HOME/lib${forwardSlash}${
            agentFileName
        }" /$
        unixScript.text = unixScript.text.replace(unixRegex, unixReplacement)
        String windowsRegex = $/"%JAVA_EXE%" %DEFAULT_JVM_OPTS%/$
        String windowsReplacement = $/"%JAVA_EXE%" %DEFAULT_JVM_OPTS% -javaagent:"%APP_HOME%\lib\$agentFileName"/$
        windowsScript.text = windowsScript.text.replace(windowsRegex, windowsReplacement)
    }
}

project.tasks.run {
    dependsOn ':db:build'
    doFirst {
        project.applicationDefaultJvmArgs += ["-javaagent:${project(':db').getBuildDir()}/libs/babblebot-server-db-${project(':db').getVersion()}.jar"]
        project.applicationDefaultJvmArgs += ["-Dname=BabbleBot-Server"]
        project.applicationDefaultJvmArgs += ["-Dio.netty.noUnsafe=true"]
    }
}
jar {
    manifest {
        attributes 'Main-Class': 'net.bdavies.BabbleBot'
    }


    dependsOn createProperties
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':server')
    implementation project(':api')
}
